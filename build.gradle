plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group 'futurelink'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

javafx {
    version = "20"
    modules = [ 'javafx.controls' ]
}

configurations.configureEach {
    resolutionStrategy {
        dependencySubstitution {
            def os = System.getProperty("os.name").toLowerCase()
            if (os.contains("windows")) {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.124.0")
            }
            else if (os.contains("linux")) {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:3.124.0")
            }
            else if (os.contains("mac")) {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.cocoa.macosx.x86_64:3.124.0")
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    implementation "com.google.guava:guava:32.0.1-jre"
    implementation 'org.apache.xmlgraphics:batik-all:1.16'
    implementation 'org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.124.0'
    implementation 'org.dom4j:dom4j:2.1.4'
    implementation 'jaxen:jaxen:2.0.0'

    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}